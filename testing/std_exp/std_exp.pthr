

func bitCast = <{To: Type}> (from: $From) #pub -> To {
	return @bitCast<{From, To}>(from);
}


func numBytes = <{T: Type, INCLUDE_PADDING: Bool}> () #pub -> USize {
	return @numBytes<{T, INCLUDE_PADDING}>();
}

func sizeOf = <{T: Type}> () #pub -> USize {
	return @numBytes<{T, true}>();
}



//////////////////////////////////////////////////////////////////////
// array ref iter


type ArrayRefIter = struct <{ARRAY_REF_TYPE: Type}> {
	type alias ELEM_TYPE = type(@arrayRefElementTypeID<{ARRAY_REF_TYPE}>());

	func new = (arr_ref_ptr: ARRAY_REF_TYPE*) -> (output: This) {
		output.current = arr_ref_ptr.data();
		output.last = arr_ref_ptr[arr_ref_ptr.size() - 1];
		return...;
	}

	func next = (this mut) -> Void {
		const new_address: USize = @bitCast<{ELEM_TYPE*, USize}>(this.current) + @numBytes<{ELEM_TYPE, true}>();
		this.current = @bitCast<{USize, ELEM_TYPE*}>(new_address);
	}

	func get = (this) -> ELEM_TYPE* { return copy this.current; }

	func atEnd = (this) -> Bool {
		return @bitCast<{ELEM_TYPE*, USize}>(this.current) > @bitCast<{ELEM_TYPE*, USize}>(this.last);
	}


	impl @pthr.Iterator {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	impl @pthr.IteratorRT {
		next  = next,
		get   = get,
		atEnd = atEnd,
	}

	var current: ELEM_TYPE*;
	var last: ELEM_TYPE*;
}


func createArrayRefIter = <{ARRAY_REF_TYPE: Type}> (arr_ref_ptr: ARRAY_REF_TYPE*) 
#pub #builtin("array_ref.Iterable.createIterator")
-> ArrayRefIter<{ARRAY_REF_TYPE}> {
	return new ArrayRefIter<{ARRAY_REF_TYPE}>(arr_ref_ptr);
}