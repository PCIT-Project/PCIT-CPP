def std = @import("std");



type TypeSystemEscape = struct <{T: Type}> {
	func get = (this) #unsafe -> T* { return std.bit.bitCast<{T*}>(this.data.data()); }
	func get = (this mut) #unsafe -> T*mut { return std.bit.bitCast<{T*mut}>(this.data.data()); }
	func getUninit = (this mut) #unsafe -> T*! { return std.bit.bitCast<{T*!}>(this.data.data()); }
	
	var data: [Byte:std.types.numBytes<{T}>()] #priv;
}


type StaticArrayList = struct <{T: Type, CAPACITY: USize}> #pub {
	func new = () -> (output: This) {
		output._data = new [TypeSystemEscape<{T}>:CAPACITY]();
		output._size = 0;
		return...;
	}

	func delete = (this mut) -> Void {
		for(this as [T:*mut]) [_; elem: T mut] {
			unsafe{ delete elem; }
		}

		delete this._data;
	}


	func copy = (this) -> (output: This) {
		output = new This();

		for(this as [T:*]) [_; elem: T]{
			output.append().* = copy elem;
		}

		return...;
	}


	func move = (this mut) -> (output: This) {
		output = new This();

		for(this as [T:*mut]) [_; elem: T mut]{
			unsafe{ output.append().* = move elem; }
		}

		this._size = 0;

		return...;
	}





	///////////////////////////////////
	// element access

	func [] = (this, index: USize) -> T* { unsafe{ return this._data[index].get(); } }
	func [] = (this mut, index: USize) -> T*mut { unsafe{ return this._data[index].get(); } }

	func first = (this) -> T* { return this[0]; }
	func first = (this mut) -> T*mut { return this[0]; }

	func last = (this) -> T* { return this[this.size() - 1]; }
	func last = (this mut) -> T*mut { return this[this.size() - 1]; }

	func data = (this) -> T* { return this[0]; }
	func data = (this mut) -> T*mut { return this[0]; }



	///////////////////////////////////
	// capacity

	func empty = (this) -> Bool { return this.size() == 0; }
	func size = (this) -> USize { return copy this._size; }
	func capacity = (this) -> USize { return CAPACITY; }


	///////////////////////////////////
	// modifiers

	func clear = (this mut) -> Void {
		for(this as [T:*mut]) [_; elem: T mut] {
			unsafe{ delete elem; }
		}

		this._size = 0;
	}


	func append = (this mut) -> T*! {
		this._size += 1;

		unsafe{ return this._data[this._size - 1].getUninit(); }
	}

	func pop = (this mut) -> Void {
		unsafe{ delete this.last().*; }
		this._size -= 1;
	}


	///////////////////////////////////
	// type conversion

	func as = (this) -> [T:*] { return new [T:*](this.data(), this.size()); }
	func as = (this mut) -> [T:*mut] { return new [T:*mut](this.data(), this.size()); }


	///////////////////////////////////
	// iterators

	// TODO(FUTURE): 


	///////////////////////////////////
	// member vars

	var _data: [TypeSystemEscape<{T}>:CAPACITY] #priv;
	var _size: USize;
}
